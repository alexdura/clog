Call(caller, call_expr, caller_name, callee_name) :- caller <: .. $fun1( .. ) { .. }:>,
	     			     		     ID($fun1, caller_name),    # function definition
						     call_expr <: $fun2( .. ) :>,
						     ANCESTOR(caller, call_expr), ID($fun2, callee_name). # call expression

# Call(caller, call_expr, caller_name, callee_name)
# when
# caller matches <: .. $fun1( .. ) { .. }:>
# and
# caller_name is ID of $fun1
# and
# call_expr matches <: $fun2( .. ) :>
# and
# caller is ANCESTOR of call_expr
# and
# callee_name is ID of $fun2


FunCall(fun2, c) :- c <: $fun2( .. ) :>, ID($fun2, fun2).

# FunCall(fun2, c)
# when
# c matches $fun2( .. )
# and
# fun2 is ID of $fun2
FunDef(fun1, d) :- d <: .. $fun1( .. ) { .. } :>, ID($fun1, fun1).


Loop(s), ForLoop(s) :- s <: for ( ; ; ) $_ :>.
Loop(s), WhileLoop(s) :- s <: while ($_) $_ :>.
Loop(s), DoWhileLoop(s) :- s <: do $_ while ($_); :>.

LoopInFunction(s, f_name) :- f <: .. $f( .. ) { .. } :>, ID($f, f_name), ANCESTOR(f, s), Loop(s).

# look for nested for loops
NestedLoops(s1, s2) :- Loop(s1), Loop(s2), NEQ(s1, s2), ANCESTOR(s1, s2).

# enclosed loops are nested loops without any loop in between
NotEnclosedLoops(s1, s2) :- NestedLoops(s1, s), NestedLoops(s, s2).
EnclosedLoops(s1, s2) :- NestedLoops(s1, s2), NOT(NotEnclosedLoops(s1, s2)).

# the call and its parent loop
# AncestorOfCall(c, a)
# when
# FunCall(_, c) // c is a function call
# a is PARENT of c
# and a is not a loop
AncestorOfCall(c, a) :- FunCall(_, c), PARENT(a, c), NOT(Loop(a)).
AncestorOfCall(c, a) :- AncestorOfCall(c, t), PARENT(a, t), NOT(Loop(a)).

# l is the loop immediately enclosing the call c
LoopForCall(l, c) :- PARENT(l, t), AncestorOfCall(c, t), Loop(l).

# debugging info
LoopForCallDetail(src, ls, le, fun) :- LoopForCall(l, c), FunCall(fun, c), SRC(l, ls, _, le, _, src).

# local loop depth
# a loop without an immediately enclosing loop has depth 0
LoopDepth(l, 0) :- Loop(l), NOT(EnclosedLoops(_, l)).
# a loop with an immediately enclosing loop has a depth +1 the depth of the enclosing loop
LoopDepth(l, i) :- LoopDepth(l_outer, depth_outer), EnclosedLoops(l_outer, l), BIND(i, depth_outer + 1).


NotMaxLoopDepth(l, i) :- LoopDepth(l, i), LoopDepth(l, j), GT(j, i).

# debugging info containing location
LoopDepthDetail(sl, sc, el, ec, f, d) :- LoopDepth(l, d), NOT(NotMaxLoopDepth(l, d)), SRC(l, sl, sc, el, ec, f).

# predicate LoopDepthDetails(sl, sc, el, ec, f, d)
# where
# d is the LoopDepth of l
# and
# d is not NotMaxLoopDepth of l
# and
# (l, sl, sc, el, ec) is SRC of l

# local loop depth for calls
LocalLoopDepthForCall(c, 0) :- FunCall(_, c), NOT(LoopForCall(_, c)).
LocalLoopDepthForCall(c, i) :- LoopForCall(l, c), LoopDepth(l, j), NOT(NotMaxLoopDepth(l, j)), BIND(i, j + 1).

# compute the transitive closure of the call relation
TransitiveCall(caller_name, callee_name) :- Call(_, _, caller_name, callee_name).
TransitiveCall(caller_name, callee_name) :- TransitiveCall(caller_name, t), Call(_, _, t, callee_name).
# if a function calls itself, then it's recursive
RecursiveFunction(fun) :- TransitiveCall(fun, fun).

# The loop depth from which a function is called. Only for non-recursive functions
# There may be multiple loop depths for a function, depending on the call stack
# Functions that are not called have loop depth of 0
GlobalLoopDepthForFunction(fun_name, 0) :- FunDef(fun_name, _), NOT(FunCall(fun_name, _)), NOT(RecursiveFunction(fun_name)).

GlobalLoopDepthForFunction(callee_name, k) :- Call(caller, call_expr, caller_name, callee_name), NOT(RecursiveFunction(callee_name)),
			      GlobalLoopDepthForFunction(caller_name, i), LocalLoopDepthForCall(call_expr, j), BIND(k, i + j).

# Compute the global loop depth. Depending on call stack, there may be more than one loop depth for a function.
# The global loop depth is the loop depth for the current function + the local loop depth
GlobalLoopDepth(l, k) :- LoopInFunction(l, f_name), LoopDepth(l, i), GlobalLoopDepthForFunction(f_name, j), BIND(k, i+j).

NotMaxGlobalLoopDepth(l, n) :- GlobalLoopDepth(l, k), GlobalLoopDepth(l, n), GT(k, n).
GlobalLoopDepthDetail(src, ls, le, depth) :- GlobalLoopDepth(l, depth), NOT(NotMaxGlobalLoopDepth(l, depth)), SRC(l, ls, _, le, _, src).

OUTPUT('GlobalLoopDepthDetail, "GlobalLoopDepthDetail.csv", "csv").
OUTPUT('LoopDepthDetail, "LoopDepthDetail.csv", "csv").
OUTPUT('RecursiveFunction, "RecursiveFunction.csv", "csv").
