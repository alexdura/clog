/**
	Program
**/
Program  ::= Clause* /FormalPredicate*/ /ExpandedClause:Clause*/;

/**
   Clause
**/
abstract Clause ::= Heads:CommonLiteral*;
Rule:Clause ::= Body:CommonLiteral*;
Fact:Clause;

/**
   Marker AST node for negated Literals
**/
Not;

/**
   Literals
**/
abstract CommonLiteral ::= [Not];

Literal:CommonLiteral ::= Predicate:PredicateSymbol Terms:Term*;
ExternalLiteral:Literal ::= <ExternalPayload:Object>;

abstract Constraint:CommonLiteral ::= Expr*;

abstract BinaryIntegerConstraint : Constraint;
EQLiteral:BinaryIntegerConstraint;
NEQLiteral:BinaryIntegerConstraint;
LTLiteral:BinaryIntegerConstraint;
LTELiteral:BinaryIntegerConstraint;
GTLiteral:BinaryIntegerConstraint;
GTELiteral:BinaryIntegerConstraint;

MATCHLiteral:Constraint;
BINDLiteral:Constraint;
ANCESTORLiteral:Constraint;

/**
	Expressions
**/
abstract Expr;
Functor : Expr ::= Func:FunctorSymbol Arg:Expr*;

/**
    Terms
**/
abstract Term:Expr;
abstract Constant:Term;
abstract CommonVariable:Term;

Variable:CommonVariable ::= <VAR_ID>;
Wildcard:CommonVariable;
PredicateRef:Constant   ::= <PRED_ID>;
StringConstant:Constant ::= <STRING>;
IntConstant:Constant    ::= <NUMERAL>;

/**
    PredicateSymbols
**/
FormalPredicate ::= <PRED_ID>;
PredicateSymbol ::= <PRED_ID>;
FunctorSymbol ::= <FUNC_ID>;

/**
   Patterns
**/
Pattern : CommonLiteral ::= <PATTERN> /Literal/ [Root:Variable] [Subtree:Variable];

/**
   Internal AST Nodes
**/
InternalLiteral : Literal;
InternalFunctor : Functor;